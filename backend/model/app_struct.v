module model

import veb
import common.api
import common.crypt { AuthPayload }
import common.reqid
import adapter.dbpool
import adapter.cache_pool
import orm
import pool
import adapter.datascope { ScopeContext }
import config
import locale

pub struct App {
	veb.Middleware[Context]
	veb.Controller
	veb.StaticHandler
pub mut:
	server          &veb.Server = unsafe { nil } // 服务器实例引用,优雅关闭服务使用
	started         chan bool // 用于通知应用程序已成功启动
	shutdown_signal chan bool // 用于触发优雅关闭
}

pub struct Context {
	veb.Context
pub mut:
	scope_sc     ScopeContext
	dbpool       &dbpool.DatabasePoolable @[noinit]
	cache_pool   ?&cache_pool.CachePool // Redis 暂不使用，改为可空字段
	config       &config.GlobalConfig
	jwt_payload  ?AuthPayload
	locale       &locale.LocaleStore
	extra_locale map[string]string = map[string]string{}
	// request_id 是本次调用的唯一标识，由 request_id_middleware 在请求入口写入；
	// 日志前缀、响应头与响应体里的 request_id 都用它。
	request_id string

	svc_iam ServiceContextIam
}

// ----- IAM 统一上下文 ---
pub struct ServiceContextIam {
pub mut:
	user_id        string
	token_jwt      string
	tenant_ids     []string
	subproduct_ids []string
	subportal_ids  []string
	apikey_id      string
	workspace_ids  []string
	// 当前请求的活跃数据范围（用于 datascope SQL 过滤）
	active_tenant_id     string
	active_subproduct_id string
	active_subportal_id  string
}

// acquire_scoped 将 ctx 上的 svc 上下文填充到 ScopeContext，委托 adapter.datascope 获取带数据范围的 DB 连接
pub fn (mut ctx Context) acquire_scoped() !(orm.DB, &pool.ConnectionPoolable) {
	ctx.scope_sc.dbpool = ctx.dbpool
	ctx.scope_sc.user_id = ctx.svc_iam.user_id
	// AK/SK: 将当前请求的租户/产品/门户信息传递到 datascope 层，用于 SQL WHERE 过滤
	if ctx.svc_iam.active_tenant_id != '' {
		ctx.scope_sc.tenant_id = ctx.svc_iam.active_tenant_id
	}
	if ctx.svc_iam.active_subproduct_id != '' {
		ctx.scope_sc.subproduct_id = ctx.svc_iam.active_subproduct_id
	}
	if ctx.svc_iam.active_subportal_id != '' {
		ctx.scope_sc.subportal_id = ctx.svc_iam.active_subportal_id
	}
	db, conn := datascope.acquire_scoped(mut ctx.scope_sc) or { return err }
	return db, conn
}

// 翻译查询的快捷方式，等价于 ctx.locale.t(key)
// 用法：ctx.t('common.success') or { '成功' }
// locale 中间件未生效（ctx.locale 为空）时返回 none，交给调用方的 or 兜底
pub fn (ctx &Context) t(key string) ?string {
	if isnil(ctx.locale) {
		return none
	}
	return ctx.locale.t(key)
}

// json 覆盖内嵌的 veb.Context.json：在编码统一响应体之前注入本次请求的 request_id。
//
// 这样业务代码保持 `ctx.json(api.json_success(...))` 写法不变，
// request_id 由请求上下文统一提供，而不是在响应构造时生成。
pub fn (mut ctx Context) json[T](j T) veb.Result {
	if ctx.request_id == '' {
		// 非 HTTP 上下文（测试/后台任务）没有入口中间件，这里兜底生成。
		ctx.request_id = reqid.generate()
	}
	mut v := j
	$if T is api.ApiSuccessResponse {
		v.request_id = ctx.request_id
	} $else $if T is api.ApiErrorResponse {
		v.request_id = ctx.request_id
	}
	return ctx.Context.json(v)
}
